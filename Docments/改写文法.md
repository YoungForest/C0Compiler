<程序> ::= ［<常量说明>］［<变量说明>］{(int|char) <标识符> <有返回值函数定义>| void <无返回值函数定义>} void <主函数>

<有返回值函数定义> ::= '(' <参数表>‘)’ ‘{’<复合语句>‘}’

<无返回值函数定义> ::= <标识符> ‘(’<参数表>‘)’‘{’<复合语句>‘}’

<主函数> ::= main‘(’‘)’ ‘{’<复合语句>‘}’

<复合语句> ::=  ［<常量说明>］［<变量说明>］<语句列>

<常量说明> ::=  const<常量定义>;{ const<常量定义>;}

<常量定义> ::=   int<标识符>＝<整数>{,<标识符>＝<整数>} | char<标识符>＝<字符>{,<标识符>＝<字符>}

<变量说明> ::= <变量定义>;{<变量定义>;}

<变量定义> ::= <类型标识符>(<标识符>|<标识符>‘[’<无符号整数>‘]’){,(<标识符>|<标识符>‘[’<无符号整数>‘]’) }

<参数表> ::=  <类型标识符><标识符>{,<类型标识符><标识符>}| <空>

<整数> ::= ［+｜-］<无符号整数>｜０

<语句> ::= <条件语句>｜<do循环语句> | <for循环语句>｜‘{’<语句列>‘}’｜<函数调用语句>;｜<赋值语句>;｜<读语句>;｜<写语句>;｜<空>;｜<返回语句>;

<条件> ::=  <表达式><关系运算符><表达式>

<条件语句> ::=  if ‘(’<条件>‘)’<语句>［else<语句>］

<do循环语句> ::=  do<语句>while ‘(’<条件>‘)’

<for循环语句> ::= for‘(’<标识符>＝<表达式>;<条件>;<标识符>＝<标识符>(+|-)<无符号整数>‘)’<语句> 

<赋值语句> ::=  <标识符>＝<表达式>|<标识符>‘[’<表达式>‘]’=<表达式>

<函数调用语句> ::= <标识符>‘(’<值参数表>‘)’

<值参数表> ::= <表达式>{,<表达式>}｜<空>

<语句列> ::=｛<语句>｝

<读语句> ::=  scanf ‘(’<标识符>{,<标识符>}‘)’

<写语句> ::=  printf‘(’<字符串>,<表达式>‘)’|printf ‘(’<字符串>‘)’|printf ‘(’<表达式>‘)’

<返回语句> ::=  return[‘(’<表达式>‘)’]

<表达式> ::= ［＋｜－］<项>{<加法运算符><项>}

<项> ::= <因子>{<乘法运算符><因子>}

<因子> ::= <标识符>｜<标识符>‘[’<表达式>‘]’｜<整数>|<字符>｜<有返回值函数调用语句>|‘(’<表达式>‘)’
